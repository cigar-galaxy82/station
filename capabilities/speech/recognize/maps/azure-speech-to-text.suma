profile = "speech/recognize@1.0"
provider = "azure-speech-to-text"

map Recognize {
  audioEncoding = 'wav'

  set if(input.audioEncoding) {
    audioEncoding = input.audioEncoding
  }

  return map error if(audioEncoding !== 'wav') {
    title = "Audio encoding not supported"
  }

  http POST "/speech/recognition/conversation/cognitiveservices/v1" {
    security "opc-subscription-key"

    request "audio/wav" {
      query {
        language = input.languageCode
      }
    
      body = Buffer.from(input.audioContent, 'base64')
    }

    response 200 {
      return map result {
        results = [{
          alternatives: [{
            transcript: body.DisplayText
          }]
        }]
      }
    }

    response 400 "text/plain" {
      map error {
        title = "Bad request"
        detail = body
      }
    }

    response 401 {
      map error {
        title = "Unauthorized"
      }
    }

    response 500 "text/plain" {
      map error {
        title = "Internal server Error"
        detail = body
      }
    }
  }
}

