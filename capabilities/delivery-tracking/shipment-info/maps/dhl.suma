//DHL API reference: https://developer.dhl.com/api-reference/shipment-tracking

profile = "delivery-tracking/shipment-info@1.1"
provider = "dhl"

map ShipmentInfo {
   call FetchShipment(
    trackingNumber = input.trackingNumber,
    carrier = input.carrier) {
      return map result if (!outcome.error) outcome.data

      return map error {
        title = outcome.error.title
      }
    }
}

operation FetchShipment {
  trackingNumber = args.trackingNumber

  http GET "/track/shipments" {
    security "dhl_api_key"
    request {
      query {
        trackingNumber = trackingNumber
        service = args.carrier
      }
    }
    response 200 "application/json" {
      shipments = call foreach(shipment of body.shipments) MapShipment(
        shipment = shipment,
        trackingNumber = trackingNumber
      )

      return shipments
    }
    response 404 {
      fail {
        title = "No shipment with given tracking number found"
      }
    }
    response 401 {
      fail {
        title = "Invalid api key"
      }
    }
    response 429 {
      fail {
        title = "Too many requests within defined time period"
      }
    }
  }
}

operation MapShipment {
  estimatedDeliveryDate = null
  shipment = args.shipment

  set if (shipment.estimatedDeliveryDate) {
    estimatedDeliveryDate = shipment.estimatedDeliveryDate
  }
  set {
    status = call MapEvent(
      event = shipment.status
    )
  }
  set {
    events = call foreach(event of shipment.events) MapEvent(
      event = event
    )
  }

  return {
    trackingNumber = args.trackingNumber
    origin = shipment.origin
    destination = shipment.destination
    status = status
    events = events
    estimatedDeliveryDate = estimatedDeliveryDate
    carrier = shipment.service
  }
}

operation MapEvent {
  set if(args.event.statusCode === "pre-transit") {
    statusCode = "pre_transit"
  }
  set if(args.event.statusCode !== "pre-transit") {
    statusCode = args.event.statusCode
  }
  
  return {
    timestamp = args.event.timestamp
    statusCode = statusCode
    statusText = args.event.status
    location = args.event.location
  }
}
