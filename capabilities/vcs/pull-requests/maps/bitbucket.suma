profile = "vcs/pull-requests@2.0"
provider = "bitbucket"

"""
PullRequests map
"""
map PullRequests{
  owner = input.owner
  repo = input.repo

  call Paginate(
  owner = input.owner,
  repo = input.repo,
  ) {
      return map error if (outcome.data.statusCode !== 200) outcome.data
      return map result { pullRequests = outcome.data.pullRequests }
    }
}

operation Paginate {
  data = []
  page = 1
  //Hack to create a while loop
  //FIX: if this is is more then 10 (eg 1000) testing lib is not able finish the snapshot recording. Looks like fail is not terminating the loop
  call foreach(_x of Array(10)) FetchPullRequests(owner = args.owner, repo = args.repo, page = page) {
    fail if (outcome.data.statusCode !== 200) outcome.data

    data = [...data, ...outcome.data.pullRequests]

    page = page + 1
    return if (outcome.data.isLastPage) {
      pullRequests: data,
      statusCode: 200
    }
  }
  return {
      pullRequests: data,
      statusCode: 200
  }
 }

operation FetchPullRequests {
  http GET "/2.0/repositories/{args.owner}/{args.repo}/pullrequests" {
    request {
      query {
        page = args.page
      }
    }
    response  {
      fail if(statusCode !== 200) {
        message: body.error.message,
        statusCode: statusCode
      }
      return {
        pullRequests: body.values.map((pr) => {
          return {
            title: pr.title,
            id: pr.id,
            url: pr.links.html.href,
            sha: pr.source.commit.hash,
          }
        }),
        statusCode: statusCode,
        isLastPage: body.next ? false : true
      }
    }
  }
}
  