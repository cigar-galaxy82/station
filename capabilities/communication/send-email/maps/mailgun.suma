profile = "communication/send-email@1.1"
provider = "mailgun"

// Mailgun API Reference: https://documentation.mailgun.com/en/latest/api_reference.html

"
Map using Mailgun API
"
map SendEmail {
  // TODO: make this input visible
  domain = input.domain
  
  body = {
    from: input.from,
    to: input.to,
    subject: input.subject,
  }

  set if (input.text) {
    body.text = input.text
  }
  
  set if (input.html) {
    body.html = input.html
  }

  http POST "/{domain}/messages" {
    security "basic"

    request "application/x-www-form-urlencoded" {
      body = body
    }

    response 200 {
      map result {
        messageId = body.id
      }
    }

    response 400 {
      map error {
        title = "Invalid inputs"
        message = body.message
      }
    }
  }
}

map SendTemplatedEmail {
  // TODO: make this input visible
  domain = input.domain

  http POST "/{domain}/messages" {
    security "basic"

    request "application/x-www-form-urlencoded" {
      body {
        from = input.from
        to = input.to
        template = input.templateId
        subject = (input.templateData || {}).subject // this is provider specific as mailgun doesn't have default subject in templates
        "h:x-mailgun-variables" = input.templateData
      }
    }

    response 200 {
      map result {
        messageId = body.id
      }
    }

    response 400 {
      map error {
        title = "Invalid inputs"
        message = body.message
      }
    }
  }
}
