"""
Send SMS Message
Send a single SMS message or retieve its status.
"""

name = "communication/send-sms"
version = "1.0.1"

"""
Send SMS Message
Send single text message
"""
usecase SendMessage unsafe {
  input {
    "Recepient of the message"
    to

    "Sender of the message"
    from

    "The text of the message"
    text
  }

  result {
    messageId
  }

  error {
    problem
    detail
    instance
  }
}

"
Message Status

Retrieve status of a sent SMS message
"
usecase RetrieveMessageStatus safe {
  input {
    messageId
  }

  result {
    deliveryStatus
  }
}

"
Identifier of Message
The identifier is provider-specific and not unique. It should be treated as an opaque value and only used in subsequent calls
"
field messageId string

"
Delivery Status of Message
Status of a sent message. Harmonized across different providers.
"
field deliveryStatus enum {
  accepted
  delivered
  seen
  unknown
  failed
}
