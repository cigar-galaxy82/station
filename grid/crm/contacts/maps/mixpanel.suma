// API Reference: https://developer.mixpanel.com/reference/profile-set
// Profile properties: https://help.mixpanel.com/hc/en-us/articles/115004708186

profile = "crm/contacts@1.0"
provider = "mixpanel"

map Update {
  return map error if (input.id === null || input.id === undefined) {
    title = "Missing field"
    detail = "ID is missing in input values"
  }

  set {
    customProperties = input.customProperties || {}
  }

  http POST "/engage" {
    request "application/x-www-form-urlencoded" {
      body {
        data = {
          $token: parameters.PROJECT_TOKEN,
          $distinct_id: input.id,
          $ip: customProperties.$ip,
          
          $set: {
            $email: input.email,
            $phone: input.phone,
            $first_name: input.firstName,
            $last_name: input.lastName,
            $country_code: input.country,

            Company: input.company,
            ...customProperties,
          },
        }
      }
    }

    response 200 "*" {
      return map result if (body === 1) {
        id = input.id
      }

      map error {
        title = "Invalid Data"
      }
    }

    response 401 {
      map error {
        title = "Unauthorized"
        detail = body.error
      }
    }

    response 403 {
      map error {
        title = "Forbidden"
        detail = body.error
      }
    }
  }
}

map Create {
  return map error {
    title = "Not supported"
    detail = "Use Update usecase instead"
  }
}

map Search {
  return map error {
    title = "Not supported"
    detail = "This usecase is not supported"
  }
}
