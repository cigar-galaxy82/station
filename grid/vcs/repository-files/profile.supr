"""
Repository Files
Access files in a hosted VCS (Git) repository.
"""
name = "vcs/repository-files"
version = "1.0.0"


"""
List Files in Directory
List files in a repository on the provided path
"""
usecase ListDirectory {
  input {
    """
    Repository
    Repository name or identifier, e.g.: `octocat/hello-worId` (without `owner`), or `hello-worId` (if `owner` is set).
    """
    repository! string!

    """
    Repository Owner
    Username of the account owning the repository.
    Optional if provided in `repository`.
    """
    owner string!

    """
    Directory Path
    Path to the directory for listing.
    Defaults to the root directory (`/`).
    """
    path string!

    """
    Reference (branch)
    Branch name or commit reference.
    Defaults to the default branch for the repository (e.g. `main` or `master`).
    """
    reference string!

    """
    Page
    For entries pagination, use value from `nextPage`.
    """
    page string!
  }

  result {
    entries [{
      """
      File name
      """
      name! string!

      """
      File path
      """
      path! string!

      """
      File type
      Whether the entry is a directory or a file
      """
      type! enum {
        file
        directory
      }

      """
      File mode 
      """
      mode string!

      """
      File size
      """
      size number!

      """
      File URL for downloading
      """
      rawUrl string!
    }]

    """
    Next page
    For paginating entries. Pass to `page` parameter.
    """
    nextPage string
  }

}
