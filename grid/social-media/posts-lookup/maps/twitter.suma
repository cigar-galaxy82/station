// Twitter API Reference: https://developer.twitter.com/en/docs/twitter-api

profile = "social-media/posts-lookup@1.2"
provider = "twitter"

map FindByHashtag {
  rateLimitBucket = 'Search Recent Tweets'

  http GET "/2/tweets/search/recent" {
    
    request {
      query {
        query = '#' + input.hashtag
        'tweet.fields' = 'id,text,created_at,attachments,referenced_tweets'
        expansions = 'attachments.media_keys,author_id'
        'media.fields' = 'duration_ms,height,media_key,preview_image_url,type,url,width,alt_text'
        'user.fields' = 'id,username'
        start_time = input.afterDate
        pagination_token = input.page
        max_results = 100
      }
      headers {
        authorization = "Bearer " + parameters.accessToken
      }
    }

    response 200 "application/json" {
      result = call MapSearchResult(body = body, headers = headers, rateLimitBucket = rateLimitBucket)

      map result result
    }

    response {
      error = call MapTwitterError(statusCode = statusCode, error = body, headers = headers, rateLimitBucket = rateLimitBucket)
      map error error
    }
  }
}

map FindByMention {
  rateLimitBucket = 'User mention timeline'

  http GET "/2/users/{input.profileId}/mentions" {

    request {
      query {
        'tweet.fields' = 'id,text,created_at,attachments,referenced_tweets'
        expansions = 'attachments.media_keys,author_id'
        'media.fields' = 'duration_ms,height,media_key,preview_image_url,type,url,width,alt_text'
        'user.fields' = 'id,username'
        start_time = input.afterDate
        pagination_token = input.page
        max_results = 100
      }
      headers {
        authorization = "Bearer " + parameters.accessToken
      }
    }

    response 200 "application/json" {
      result = call MapSearchResult(body = body, headers = headers, rateLimitBucket = rateLimitBucket)

      map result result
    }

    response {
      error = call MapTwitterError(statusCode = statusCode, error = body, headers = headers, rateLimitBucket = rateLimitBucket)
      map error error
    }
  }
}

operation MapSearchResult {
  body = args.body
  headers = args.headers
  posts = []

  set if (body.meta.result_count > 0) {
    //workaround for map interpreter variables merging issue
    posts = call foreach(index of Array(body.data.length - 1).fill().map((_, i) => i)) MapTweetToPost(
      index = index,
      tweets = body.data,
      includes = body.includes
    )
  }

  return {
    posts = posts
    previousPage = body.meta.previous_token
    nextPage = body.meta.next_token
    rateLimit =  call MapRateLimit(headers = headers, bucket = args.rateLimitBucket)
  }
}

operation MapTweetToPost {
  index = args.index
  tweet = args.tweets[index]
  includes = args.includes
  attachments = []
  replyReference = undefined
  replyId = undefined
  author = undefined

  set if(tweet.attachments && tweet.attachments.media_keys && includes.media) {
    attachments = call foreach(mediaKey of tweet.attachments.media_keys) MapMediaAttachment(
        mediaKey = mediaKey,
        media = includes.media
      )
  }

  set if(tweet.referenced_tweets) {
    replyReference = tweet.referenced_tweets.find(referencedTweet => referencedTweet.type === 'replied_to')
  }

  set if(replyReference !== undefined) {
    replyId = replyReference.id
  }

  set if(tweet.author_id && includes.users) {
    author = call MapUser(user = includes.users.find(user => user.id === tweet.author_id))
  }

  return {
    id = tweet.id
    url = 'https://twitter.com/i/status/' + tweet.id
    createdAt = tweet.created_at
    text = tweet.text
    replyId = replyId
    author = author
    attachments = attachments.filter(attachment => attachment !== undefined)
  }
}

operation MapMediaAttachment {
  mediaKey = args.mediaKey
  mediaList = args.media
  result = undefined

  foundMedia = mediaList.find(media => media.media_key === mediaKey)

  duration = undefined

  set if(foundMedia && foundMedia.duration_ms) {
    duration = foundMedia.duration_ms / 1000
  }

  set if(foundMedia) {
    result = {
      type: foundMedia.type,
      url: foundMedia.url,
      height: foundMedia.height,
      width: foundMedia.width,
      preview: foundMedia.preview_image_url,
      altText: foundMedia.alt_text,
      duration: duration
    }
  }

  return result
}

//Twitter rate limits: https://developer.twitter.com/en/docs/twitter-api/rate-limits
operation MapRateLimit {
  totalRequestsHeader = args.headers['x-rate-limit-limit']
  remainingRequestsHeader = args.headers['x-rate-limit-remaining']
  resetTimestampHeader = args.headers['x-rate-limit-reset']

  totalRequests = undefined
  remainingRequests = undefined
  remainingRequestsPercentage = undefined
  resetTimestamp = undefined

  set if(totalRequestsHeader && parseInt(totalRequestsHeader, 10) !== 'NaN') {
    totalRequests = parseInt(totalRequestsHeader, 10)
  }

  set if(remainingRequestsHeader && parseInt(remainingRequestsHeader, 10) !== 'NaN') {
    remainingRequests = parseInt(remainingRequestsHeader, 10)
  }

  set if(totalRequests > 0 && remainingRequests !== undefined) {
    remainingRequestsPercentage = remainingRequests / totalRequests * 100
  }

  set if(resetTimestampHeader && parseInt(resetTimestampHeader, 10) !== 'NaN') {
    resetTimestamp = parseInt(resetTimestampHeader, 10)
  }  

  return {
    bucket = args.bucket
    totalRequests = totalRequests
    remainingRequests = remainingRequests
    remainingRequestsPercentage = remainingRequestsPercentage
    resetTimestamp = resetTimestamp
  }
}

operation MapTwitterError {
  statusCode = args.statusCode
  twitterError = args.error
  headers = args.headers
  rateLimitBucket = args.rateLimitBucket

  rateLimit =  call MapRateLimit(headers = headers, bucket = rateLimitBucket)

  return if (statusCode === 400) {
    title = "Bad request"
    detail = twitterError.detail
    code = 'BadRequest'
    rateLimit = rateLimit
  }

  return if (statusCode === 401) {
    title = "Unauthenticated"
    detail = twitterError.detail
    code = 'Unauthenticated'
    rateLimit = rateLimit
  }

  return if (statusCode === 403) {
    title = "Unauthorized"
    detail = twitterError.detail
    code = 'Unauthorized'
    rateLimit = rateLimit
  }

  return if (statusCode === 404) {
    title = "Not found"
    detail = twitterError.detail
    code = 'NotFound'
    rateLimit = rateLimit
  }

  return if (statusCode === 429) {
    title = "Rate limit exceeded"
    detail = `Twitter API resource '${rateLimit.bucket}' reached max requests quota.`
    code = 'RateLimitReached'
    rateLimit = rateLimit
  }

  return {
    title = "Unknown error"
    detail = (`Unknown error occurred. Status: ${statusCode}`)
    code = 'UnknownError'
    rateLimit = rateLimit
  }
}

operation MapUser {
  author = undefined

  set if(args.user) {
    author = {
      id: args.user.id,
      username: args.user.username
    }
  }

  return author
}
