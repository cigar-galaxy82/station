name = "chat/get-messages"
version = "1.0.0"

"""
GetMessages usecase
Retrieve paginated list of messages
"""
usecase GetMessages {
	input {
		"""
		Destination
		Identification of destination to retrieve messages from
		"""
		destination! string!
		
		"""
		AfterDate
		Date that marks beginning of messages range until current time
		"""
		afterDate string
        
		"""
		Limit
		Maximum number of records to retrieve
		"""
		limit number
		
		"""
		Page
		Identification of page to retrieve
		"""
		page string
	}
	
	result Messages
	error Error

	example Successfull {
		input {
			destination = 'general',
			limit = 2
		}

		result {
			messages = [
				{
					id = '1'
					author = 'U01',
					text = 'success!'
				}
			]
		}
	}

	example Failed {
		input {
			destination = 'not-existing',
		}

		error {
			title = 'channel_not_found'
		}
	}
}

model Message {
	"""
	Id
	Identification of message
	"""
	id! string!

	"""
	Author
	Author of retrieved message
	"""
	author! string!

	"""
	Destination
	Destination of retrieved message
	"""
	destination string!

	"""
	ThreadId
	Thread destination of retrieved message
	"""
	threadId string

	"""
	Text
	Content of retrieved message
	"""
	text string

	"""
	CreatedAt
	Date retrieved message was created
	"""
	createdAt string

	"""
	CreatedAt
	Date retrieved message was last updated
	"""
	updatedAt string

	"""
	Reactions
	List of reactions on retrieved message
	"""
	reactions [
		{
			emoji string
			count number
            users [string]
		}
	]
}

model Messages {
	"""
	PreviousPage
	Identification of previous page
	"""
	previousPage string
	
	"""
	NextPage
	Identification of next page
	"""
	nextPage string

	"""
	Messages
	List of retrieved messages
	"""
	messages [Message]
}

model Error {
	"""
    Title
    A short, human-readable summary of the problem type.
    """
    title!

    """
    A human-readable explanation specific to this occurrence of the problem.
    """
	detail
}
