"""
Manage email templates
List, create, update and get template content
"""

name = "communication/email-templates"
version = "1.0.1"

"""
List all templates
Result isn't paginated, amount of returned templates depenends on provider.
"""
usecase ListTemplates safe {
  result [Template]
}

"""
Get template content
Obtain template content
"""
usecase GetTemplateContent safe {
  input {
    id!
  }

  result {
    subject
    text
    html
  }
}

"""
Create template
Creates new template
"""
usecase CreateTemplate unsafe {
  input {
    name!
    subject!
    text!
    html!
  }

  result Template
}

"""
Update template
Updates template with specified id
"""
usecase UpdateTemplate unsafe {
  input {
    id!
    name
    subject
    text
    html
  }

  result Template
}

"""
Template
Minimal information about the template
"""
model Template {
  id
  name
}

"""
Id
Unique template identifier
"""
field id

"""
Name
Name of the template
"""
field name

"""
Subject
Subject of the email
"""
field subject

"""
Text Content
Text version of the template
"""
field text

"""
HTML Content
HTML version of the template
"""
field html
