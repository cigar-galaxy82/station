"""
Manage email templates
List, create, update and get template content
"""

name = "communication/email-templates"
version = "1.0.2"

"""
List all templates
Result isn't paginated, amount of returned templates depenends on provider.
"""
usecase ListTemplates safe {
  result [Template]

  example Successful {
    result [
      {
        id = "d-8be96c63de164e6799c5dbf2521d7959"
        name = "Email template name"
      }
    ]
  }
}

"""
Get template content
Obtain template content
"""
usecase GetTemplateContent safe {
  input {
    id!
  }

  result {
    subject
    text
    html
  }

  example Successful {
    input {
      id = "d-ef6d417300f74f96a9f722050edae85f"
    }

    result {
      subject = "Station test"
      text = "Station usecase automated test

From: {{from}}
to: {{to}}

Unsubscribe ( {{{unsubscribe}}} ) - Unsubscribe Preferences ( {{{unsubscribe_preferences}}} )"
      html = """<!doctype html>
<html>

<head>
  <meta name="viewport" content="width=device-width">
  <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
  <title>Integration Test Email #1</title>
</head>

<body class="">
This template is used by integration tests only
</body>

</html>
"""
    }
  }
}

"""
Create template
Creates new template
"""
usecase CreateTemplate unsafe {
  input {
    name!
    subject!
    text!
    html!
  }

  result Template

  example Successful {
    input {
      name = "Station Create template test (DELETE ME)"
      subject = "Test Email"
      text = "This template is used by integration tests only"
      html = """<!doctype html>
<html>

<head>
  <meta name="viewport" content="width=device-width">
  <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
  <title>Integration Test Email #1</title>
</head>

<body class="">
This template is used by integration tests only
</body>

</html>
"""
    }

    result {
      id = "d-8be96c63de164e6799c5dbf2521d7959"
      name = "Station Create template test (DELETE ME)"
    }
  }
}

"""
Update template
Updates template with specified id
"""
usecase UpdateTemplate unsafe {
  input {
    id!
    name
    subject
    text
    html
  }

  result Template

  example Successful {
    input {
      name = "Station Update template test (DELETE ME)"
      subject = "Test Email"
      text = "This template is used by integration tests only"
      html = """<!doctype html>
<html>

<head>
  <meta name="viewport" content="width=device-width">
  <meta http-equiv="Content-Type" content="text/html; charset=UTF-8">
  <title>Integration Test Email #1</title>
</head>

<body class="">
This template is used by integration tests only 2
</body>

</html>
"""
    }

    result {
      id = "d-8be96c63de164e6799c5dbf2521d7959"
      name = "Station Update template test updated (DELETE ME)"
    }
  }
}

"""
Template
Minimal information about the template
"""
model Template {
  id
  name
}

"""
Id
Unique template identifier
"""
field id

"""
Name
Name of the template
"""
field name

"""
Subject
Subject of the email
"""
field subject

"""
Text Content
Text version of the template
"""
field text

"""
HTML Content
HTML version of the template
"""
field html
