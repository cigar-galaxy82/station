"""
Create Product
Creates a product in a payment system (e.g. PayPal, Stripe...)
"""
name = "payments/create-product"
version = "1.0.0"

"""
Create Product
Creates a product in a payment system.
"""

usecase CreateProduct unsafe {
  input {
    """
    Name
    Name of the product to be created.
    """
    name! string!

    """
    Description
    Description of the product to be created.
    """
    description! string!

    """
    Product type
    Indicates whether the product is physical or intangible (e.g. a service, or digital goods).

    Possible values:
      - `physical`
      - `digital`
      - `service`
    """
    type enum {
      physical,
      digital,
      service
    }
  }

  result {
    """
    ID
    Unique identifier of the product.
    """
    productId! string!
    """
    Product URL
    A URL of the product's publicly-accessible webpage.
    """
    url string
  }

  error {
    """
    Title
    A short, human-readable summary of the problem type.
    """
    title! string

    """
    Detail
    A human-readable explanation specific to this occurrence of the problem.
    """
    detail string

    """
    Code
    Machine processable error code.
    """
    code! ErrorCode!
  }

  example Successful {
    input {
      name = "Superface milk"
      description = "Milk that gives you super-powers, delivered to your door."
    }

    result {
      productId = "PROD-1192810198"
      url = "https://www.example.com/shop/PROD-1192810198"
    }
  }
}

model ErrorCode enum {
  """
  Invalid input
  Invalid input was provided, see `detail`.
  """
  InvalidInput

  """
  Unknown error
  Returned when an unexpected error occured.
  """
  UnknownError
}