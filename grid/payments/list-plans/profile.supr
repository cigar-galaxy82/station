name = "payments/list-plans"
version = "1.0.0"

"""
List subscription plans
"""

usecase ListPlans safe {
  result [
    {
      """
      ID
      Unique identifier of the subscription plan.
      """
      id string!

      """
      Name
      Name of the subscription plan.
      """
      name string!
      
      """
      Interval
      Frequency of the interval at which the customer is charged.

      Possible values:
      - `day`
      - `week`
      - `month`
      - `year`
      """
      interval enum {
        day, week, month, year
      }

      """
      Price
      Amount charged each `interval` cycle.
      """
      price number!

      """
      Currency code
      Three-letter currency code in which `price` is specified, in ISO 4217 format.
      """
      currency string!
    }
  ]
  

  error {
    """
    Title
    A short, human-readable summary of the problem type.
    """
    title! string

    """
    Detail
    A human-readable explanation specific to this occurrence of the problem.
    """
    detail string
  }

  example Successful {
    result [
      {
        id = "PLAN-89101829211"
        name = "Monthly subscription to Superface milk delivery"
        interval = "month"
        price = 25
        currency = "USD"
      },
      {
        id = "PLAN-89101829212"
        name = "Daily subscription to Superface pizza delivery"
        interval = "day"
        price = 5
        currency = "USD"
      }
    ]
  }
}