name = "payments/create-plan"
version = "1.0.0"

"""
Create plan usecase
"""

usecase CreatePlan unsafe {
  input {
    """
    Product ID
    The ID of the product to create a subscription plan for.
    (To be created with `payments/create-product`)
    """
    productId! string!

    """
    Name
    Name of the subscription plan
    """
    name! string!

    """
    Interval
    Frequency of the interval at which the customer is charged.

    Possible values:
    - `day`
    - `week`
    - `month`
    - `year`
    """
    interval! enum {
      day, week, month, year
    }!

    """
    Price
    Amount charged each `interval` cycle.
    """
    price! number!

    """
    Currency code
    Three-letter currency code in which `price` is specified, in ISO 4217 format.
    """
    currency! string!
 }

  result {
    """
    Plan ID
    ID of plan.
    """
    id! string!
    
    """
    State
    State of the plan.

    Possible values:
     - `created` - The plan was just created. It may not be possible to accept new subscriptions yet.
     - `active` - The plan is active and accepting new subscriptions.
     - `inactive` - The plan has been deactivated, and cannot accept new subscriptions.
    """
    state! enum {
      created, active, inactive
    }
  }

  error {
    """
    Title
    A short, human-readable summary of the problem type.
    """
    title! string

    """
    Detail
    A human-readable explanation specific to this occurrence of the problem.
    """
    detail string
  }

  example Successful {
    input {
      productId = "PROD-1192810198"
      name = "Monthly subscription to Superface milk delivery"
      interval = "month"
      price = 25
      currency = "USD"
    }

    result {
      id = "PLAN-89101829211"
      state = "active"
    }
  }
}