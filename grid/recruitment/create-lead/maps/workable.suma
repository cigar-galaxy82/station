profile = "recruitment/create-lead@1.0"
provider = "workable"

map CreateLead {
  return map error if (!input.jobId) {
    title = "Missing required field"
    detail = "Field jobId is required. Please provide it in input."
    code = "BadRequest"
  }

  http POST "/spi/v3/jobs/{input.jobId}/candidates" {
    security "bearer_token"

    request {
      query {
        stage = 'sourced'
      }

      headers {
        'X-WORKABLE-CLIENT-ID' = parameters.CLIENT_ID
      }

      body {
        firstname = input.firstName
        lastname = input.lastName

        email = input.email
        address = input.address
        phone = input.phone

        education_entries = input.education ? input.education.map(edu => ({
          school: edu.school,
          degree: edu.degree,
          field_of_study: edu.fieldOfStudy,
          start_date: edu.startedAt,
          end_date: edu.endedAt,
        })) : undefined
        
        experience_entries = input.workExperience ? input.workExperience.map(work => ({
          title: work.position,
          summary: work.summary,
          company: work.company,
          industry: work.industry,
          current: work.current,
          start_date: work.startedAt,
          end_date: work.endedAt,
        })) : undefined
        
        social_profiles = input.links ? input.links.map(link => ({
          type: link.name,
          url: link.url
        })) : undefined

        resumeUrl = input.cv ? input.cv.url ? input.cv.url : undefined : undefined
        resume = input.cv ? input.cv.data ? input.cv : undefined : undefined

        domain = Array.isArray(input.source) ? input.source.join(', ') : input.source
      }
    }

    response 201 "application/json" {
      map result {
        id = body.candidate.id
        jobId = body.candidate.job.shortcode
        rateLimit = call MapRateLimit(headers = headers)
      }
    }

    response "application/json" {
      error = call MapWorkableError(
        statusCode = statusCode,
        error = body.error,
        headers = headers
      )
      
      map error error
    }

    response {
      map error {
        title = "Unknown error"
        detail = JSON.stringify(body, null, 2)
        code = 'UnknownError'
      }
    }
  }
}

// Workable rate limits docs: https://workable.readme.io/reference/rate-limits
operation MapRateLimit {
  totalRequests = parseInt(args.headers['x-rate-limit-limit'], 10)
  remainingRequests = parseInt(args.headers['x-rate-limit-remaining'], 10)
  remainingRequestsPercentage = undefined
  resetTimestamp = parseInt(args.headers['x-rate-limit-reset'], 10)

  set if(isNaN(totalRequests)) {
    totalRequests = undefined
  }

  set if(isNaN(remainingRequests)) {
    remainingRequests = undefined
  }

  set if(totalRequests > 0 && remainingRequests !== undefined) {
    remainingRequestsPercentage = remainingRequests / totalRequests * 100
  }

  set if(isNaN(resetTimestamp, 10)) {
    resetTimestamp = undefined
  }

  return {
    bucket = 'workable'
    totalRequests = totalRequests
    remainingRequests = remainingRequests
    remainingRequestsPercentage = remainingRequestsPercentage
    resetTimestamp = resetTimestamp
  }
}

operation MapWorkableError {
  statusCode = args.statusCode
  workableError = args.error

  rateLimit = call MapRateLimit(headers = args.headers)

  return if (statusCode === 400) {
    title = 'Bad request'
    detail = workableError
    code = 'BadRequest'
    rateLimit = rateLimit
  }

  return if (statusCode === 401) {
    title = 'Unauthenticated'
    detail = workableError
    code = 'Unauthenticated'
    rateLimit = rateLimit
  }

  return if (statusCode === 403) {
    title = 'Unauthorized'
    detail = workableError
    code = 'Unauthorized'
    rateLimit = rateLimit
  }

  return if (statusCode === 404) {
    title = 'Not found'
    detail = workableError
    code = 'NotFound'
    rateLimit = rateLimit
  }

  return if (statusCode === 429) {
    title = 'Rate limit exceeded'
    detail = 'You reached max requests quota.'
    code = 'RateLimitReached'
    rateLimit = rateLimit
  }

  return {
    title = "Unknown error"
    detail = workableError
    code = 'UnknownError'
    rateLimit = rateLimit
  }
}
