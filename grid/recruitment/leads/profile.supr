"""
Manage Leads
Fetch, list and create sourced leads in ATS (Applicant Tracking System).
"""
name = "recruitment/leads"
version = "1.0.0"

"""
Create Lead
Creates a possible candidate either for some specific job or just for ATS.
"""
usecase CreateLead {

  input {
    """
    Job Id
    Id of a job, to which lead might apply to.
    """
    jobId string!

    """
    First Name
    First name of a lead.
    """
    firstName! string!

    """
    Last Name
    Last name of a lead.
    """
    lastName! string!

    """
    Email
    Email of a lead.
    """
    email! string!

    """
    Phone
    Phone number of a lead.
    """
    phone string

    """
    Address
    Address of a lead.
    """
    address string	

    """
    Education
    Array of education experiences.
    """
    education [Education]

    """
    Work Experience
    Array of work experiences.
    """
    workExperience [WorkExperience]

    """
    CV (Resume)
    Object containing either URL to a CV or data encoded in Base64 format with CV document and name with the file name.
    """
    cv { url string } | { name string, data string }

    """
    Links
    Array of links to different profiles or portfolios of a lead.
    """
    links [Link]

    """
    Source
    Name or array of names, with sources from where this lead comes from.
    """
    source string | [string]
  }

  result {
    """
    Id
    Unique identifier of newly created lead.
    """
    id! string!

    """
    Job Id
    Unique identifier of a job, newly created lead might apply for.
    """
    jobId string!

    """
    Rate limit
    Rate limit details.
    """
    rateLimit RateLimit
  }!

  error Error

  example Successful {
    input {
      jobId = 'JOB_ID'
      firstName = 'Demo'
      lastName = 'Testing'
      email = 'demo_testing@fakemail.com'
    }

    result {
      id = 'CANDIDATE_ID'
      jobId = 'JOB_ID'
    }
  }

  example Failed {
    input {
      jobId = 'NOT_EXISTING_JOB_ID'
      firstName = 'Demo'
      lastName = 'Testing'
      email = 'demo_testing@fakemail.com'
    }

    error {
      title = 'Not found'
      detail = 'Not found'
      code = 'NotFound'
      rateLimit = {
        bucket = 'workable'
      }
    }
  }
}

"""
Education
Object representing educational experience of a lead.
"""
model Education {
  """
  Id
  Unique identifier of lead's education experience.
  """
  id string
  
  """
  School
  Name of a school.
  """
  school string

  """
  Degree
  Degree aquired by finishing lead's education.
  """
  degree string

  """
  Field of Study
  Name of a field studied in lead's education.
  """
  fieldOfStudy string
  
  """
  Started At
  ISO8601 timestamp when the education started.
  """
  startedAt string

  """
  Ended At
  ISO8601 timestamp when the education ended.
  """
  endedAt string
}

"""
Work Experience
Object representing work experience of a lead.
"""
model WorkExperience {
  """
  Id
  Unique identifier of lead's work experience.
  """
  id string

  """
  Position
  Name of position, lead has occupied.
  """
  position string

  """
  Summary
  Summary of lead's work experience. 
  """
  summary string

  """
  Company
  Name of company.
  """
  company string

  """
  Industry
  Name of industry, the company worked in.
  """
  industry string

  """
  Current
  Boolean representing whether lead still work there.
  """
  current boolean

  """
  Started At
  ISO8601 timestamp when lead started working.
  """
  startedAt string

  """
  Ended At
  ISO8601 timestamp when lead stoped working.
  """
  endedAt string
}

"""
Link
Object representing a link sourced with lead.
"""
model Link {
  """
  Name
  Descriptive name of a link to some URL.
  """
  name string

  """
  URL
  Actual URL destination of the link.
  """
  url string
}

model Lead {
  """
  Id
  Unique identifier of a lead
  """
  id! string!
  
  """
  Job Id
  Unique identifier of a job, lead might apply for.
  """
  jobId string!

  """
  Job Title
  Title of a job, lead might apply for.
  """
  jobTitle string!

  """
  First Name
  First name of a lead.
  """
  firstName! string!

  """
  Last Name
  Last name of a lead.
  """
  lastName! string!

  """
  Full Name
  Full name of a lead.
  """
  fullName string

  """
  Email
  Email of a lead.
  """
  email! string!

  """
  Phone
  Phone number of a lead.
  """
  phone string

  """
  Address
  Address of a lead.
  """
  address string	

  """
  Links
  Array of sourced links relevant to lead. Can contain links to social profiles, portfolios or other.
  """
  links [Link]

  """
  Experiences
  Array of sourced work experiences relevant to lead.
  """
  experiences [WorkExperience]
  
  """
  Education
  Array of sourced education experiences relevant to lead.
  """
  education [Education]

  """
  CV (Resume)
  Object containing either url to a CV or data of a CV.
  """
  cv { url string } | { name string, data string }

  """
  CV Url
  Url pointing to CV of a lead.
  """
  cvUrl string

  """
  Source
  Name or array of names, with sources from where this lead comes from.
  """
  source string | [string]

  """
  Created At
  ISO8601 timestamp when was a lead created.
  """
  createdAt string
  
  """
  Updated At
  ISO8601 timestamp when was a lead lastly updated.
  """
  updatedAt string
}

model Error {
  """
  Error Title
  A short, human-readable summary of the problem type.
  """
  title!
  """
  Error Detail
  A human-readable explanation specific to this occurrence of the problem.
  """
  detail
  """
  Code
  Error code.
  """
  code! ErrorCode!
  """
  Rate limit
  Rate limit details.
  """
  rateLimit RateLimit
}

model RateLimit {
  """
  Bucket
  Different parts of API may have different rate limits.
  Bucket identifies to which part of API the rate limits apply.
  """
  bucket string
  """
  Total requests
  Total requests available in the time window.
  """
  totalRequests number
  """
  Remaining requests
  Remaining requests available in the time window.
  """
  remainingRequests number
  """
  Remaining requests available in percents
  Remaining requests available in the time window in percents.
  """
  remainingRequestsPercentage number
  """
  Reset timestamp
  Timestamp when the rate limits will reset (in  Unix Timestamp format). Available only for providers without rolling rate limit window.
  """
  resetTimestamp number
}

model ErrorCode enum {
  """
  Bad request
  The request was invalid or cannot be otherwise served.
  """
  BadRequest
  """
  Not found
  The profileId does not exist.
  """
  NotFound
  """
  Rate limit reached
  Returned when a request cannot be served due to the user or app reached max requests quota.
  """
  RateLimitReached
  """
  Unauthenticated
  There was a problem authenticating your request. Check that you are passing valid credentials.
  """
  Unauthenticated
  """
  Unauthorized
  The request is understood, but it has been refused or access is not allowed.
  """
  Unauthorized
  """
  Unknown error
  Returned when an unexpected error occured.
  """
  UnknownError
  """
  Conflict
  The request can not be executed, it is in conflict with current state of the target resource.
  """
  Conflict
}
